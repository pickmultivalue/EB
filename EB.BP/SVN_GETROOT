    FUNCTION SVN_GETROOT(FilePath)
!
! Return the root path tree above path recognised by SVN
!
    DEFFUN GETFULLPATH()
    DEFFUN SVN_GETPWD()
    DEFFUN SVN_CASE()
    INCLUDE JBC.h
    EQU TRUE TO 1
!
    INCLUDE EB.INCLUDES SVN_DEBUG
    ChildPath = SVN_GETPWD(FilePath, DIR_DELIM_CH, TRUE)
    FullPath = GETFULLPATH(FilePath)
!
! Reverse travers both tFilePath and tRepo
! to match up (and remove) the trailing dirs
!
    FullPath = CHANGE(FullPath, DIR_DELIM_CH, @AM)
    ChildPath = CHANGE(ChildPath, DIR_DELIM_CH, @AM)
!
! Just in case (Windows) make case insensitive versions
!
    c_ChildPath = SVN_CASE(ChildPath)
    c_FullPath = SVN_CASE(FullPath)
    ChildPathCNT = DCOUNT(c_ChildPath, @AM)
    FullPathCNT = DCOUNT(c_FullPath, @AM)
    LOOP WHILE SVN_CASE(c_ChildPath<ChildPathCNT>) = SVN_CASE(c_FullPath<FullPathCNT>) AND FullPathCNT > 1 DO
        ChildPathCNT--
        FullPathCNT--
    REPEAT
!
    RETURN CHANGE(FIELD(FullPath, @AM, 1, FullPathCNT), @AM, DIR_DELIM_CH)
