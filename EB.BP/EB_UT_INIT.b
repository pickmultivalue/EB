    SUBROUTINE EB_UT_INIT
    INCLUDE EB.EQUS EB.COMMON
* @(#) EB.UT.INIT Ported to jBASE 16:15:17  27 JUL 2001
    INCLUDE EB.EQUS TERM.TYPES
    INCLUDE EB.EQUS EB.CHARS
    INCLUDE EB.EQUS EB.EQUS
    INCLUDE EB.EQUS USER_SETTINGS
    EQU AM TO CHAR(254),VM TO CHAR(253),SVM TO CHAR(252)
    MAT OTHER.PARAMS=''
    IF UNASSIGNED(GEX(1)) THEN
        MAT GEX=''
        OPEN 'MD' TO F.MD ELSE STOP 201,'MD'
    END
    IF UNASSIGNED(EXTRAS(1)) THEN MAT EXTRAS=''
    INCLUDE EB.EQUS OTHER.PARAMS
    INCLUDE EB.OS.INCLUDES OS.ERRORS
    OPEN 'EB.PARAMS' TO FG$EB.PARAMS ELSE STOP OPER,'EB.PARAMS'
    OPEN 'EB.SECURITY' TO FG$SECURITY ELSE STOP OPER,'EB.SECURITY'
    OPEN 'EB.TERM-TYPES' TO TERM.FILE ELSE STOP OPER,'EB.TERM-TYPES'
    OPEN 'EB.CONTROL' TO FG$EB.CONTROL ELSE FG$EB.CONTROL=FG$EB.PARAMS
!
    INCLUDE EB.OS.INCLUDES WHO
    MATREAD User_Settings FROM FG$EB.CONTROL,FG$LOGNAME:'*SETTINGS' ELSE
        MATREAD User_Settings FROM FG$EB.CONTROL,'GLOBAL*SETTINGS' ELSE
            MAT User_Settings = ''
        END
    END
    ReplMode=US_ReplaceMode
    RegInput=NOT(US_EditInput)
    ConfirmExit=NOT(US_EscExit)
    MAT TERM.TYPES=SYSTEM(7)
    INCLUDE EB.OS.INCLUDES TERM.SETTINGS
    INCLUDE EB.OS.INCLUDES SKIP.PASSWORD
    READ EB.SECURITY.REC FROM FG$SECURITY,FG$LOGNAME ELSE
        READ EB.SECURITY.REC FROM FG$SECURITY,FG$TUSER ELSE EB.SECURITY.REC=''
    END
    IF LEN(EB.SECURITY.REC)#0 THEN
        UserName = EB.SECURITY.REC<1>
        FG$MONITOR.SECS=EB.SECURITY.REC<8>
        FG$TIMEOUT=EB.SECURITY.REC<9>*60
    END ELSE UserName = FG$LOGNAME
    MATREAD SCREEN.PARAMS FROM FG$EB.PARAMS,'CRT@':TERM.TYPE ELSE MAT SCREEN.PARAMS=''
    CURS.ON=SCREEN.PARAMS(22)
    CURS.OFF=SCREEN.PARAMS(23)
    CURS.BLOCK=SCREEN.PARAMS(54)
    CURS.LINE=SCREEN.PARAMS(55)
    CRT.COLS=SCREEN.PARAMS(48)
    IF RegInput THEN
        BG=''
        FG=''
    END
    MATREAD EB$CHARS FROM FG$EB.PARAMS,'EB.CHARS@':KEYBOARD.TYPE ELSE
        ABORT 202,'EB.PARAMS EB.CHARS@':KEYBOARD.TYPE
    END
    IF FG$BS.CH='' THEN FG$BS.CH=CHAR(8)
    FG$TERM.TYPE=TERM.TYPE
    FG$KEYBOARD=KEYBOARD.TYPE
    READ FG$MNENOMICS FROM FG$EB.PARAMS,'KEY.MNENOMICS@':FG$KEYBOARD ELSE FG$MNENOMICS=''
    FG$DISK.DRIVE=DISK.DRIVE<1,1>
    INCLUDE EB.INCLUDES INPUT.CODES
    OPEN 'EB.SECURITY' TO FG$SECURITY ELSE STOP OPER,'EB.SECURITY'
    IF FG$TIMEOUT ELSE FG$TIMEOUT=1200
    IF FG$MONITOR.SECS ELSE FG$MONITOR.SECS=FG$TIMEOUT
    READV FG$HOT.KEYS FROM FG$EB.CONTROL,FG$TUSER:'*HOT.KEYS',1 ELSE NULL
    READ FG$DAYS FROM FG$EB.PARAMS,'DAYS@':FG$LANGUAGE ELSE FG$DAYS=''
    READ FG$MONTHS FROM FG$EB.PARAMS,'MONTHS@':FG$LANGUAGE ELSE FG$MONTHS=''
    READ FUNCTIONS FROM FG$EB.PARAMS,'FUNC@':FG$KEYBOARD THEN
        FG$EXPECT.CR=FUNCTIONS<20>
        FG$FUNC.CHARS=FUNCTIONS<11>
        FG$FUNC.CHARS<1,-1>=FUNCTIONS<16>
    END
    READ FUNCTIONS FROM FG$EB.PARAMS,'FUNC@':FG$KEYBOARD:'@' THEN
        FG$FUNC.VALS=FUNCTIONS<9>
        FG$FUNC.VALS<1,-1>=FUNCTIONS<15>
    END
    READ EB.HELP FROM FG$EB.PARAMS, 'EB.HELP' THEN
        EB.HELP = CHANGE(EB.HELP, AM, VM)
    END ELSE EB.HELP = ''
    MAT CRT.PARAMS=''
    RETURN
 END
