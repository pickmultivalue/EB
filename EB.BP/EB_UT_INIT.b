    SUBROUTINE EB_UT_INIT
    INCLUDE EB.EQUS EB.COMMON
    INCLUDE EB.EQUS TERM.TYPES
    INCLUDE EB.EQUS EB.CHARS
    INCLUDE EB.EQUS EB.EQUS
    INCLUDE EB.EQUS USER_SETTINGS
    EQU AM TO CHAR(254),VM TO CHAR(253),SVM TO CHAR(252)
    MAT OTHER.PARAMS=''
    DEFFUN EBGETHOME()
    IF UNASSIGNED(GEX(1)) THEN
        MAT GEX=''
    END
    IF UNASSIGNED(EXTRAS(1)) THEN MAT EXTRAS=''
    INCLUDE EB.EQUS OTHER.PARAMS
    INCLUDE EB.OS.INCLUDES OS.ERRORS
    path = EBGETHOME()
    OPEN path:'EB.PARAMS' TO FG_EB.PARAMS ELSE STOP OPER,'EB.PARAMS'
    OPEN path:'EB.SECURITY' TO FG_SECURITY ELSE STOP OPER,'EB.SECURITY'
    OPEN path:'EB.TERM-TYPES' TO TERM.FILE ELSE STOP OPER,'EB.TERM-TYPES'
    OPEN path:'EB.CONTROL' TO FG_EB.CONTROL ELSE FG_EB.CONTROL=FG_EB.PARAMS
!
    INCLUDE EB.OS.INCLUDES WHO
    MATREAD User_Settings FROM FG_EB.CONTROL,FG_LOGNAME:'*SETTINGS' ELSE
        MATREAD User_Settings FROM FG_EB.CONTROL,'GLOBAL*SETTINGS' ELSE
            MAT User_Settings = ''
        END
    END
    ReplMode=US_ReplaceMode
    RegInput=NOT(US_EditInput)
    ConfirmExit=NOT(US_EscExit)
    MAT TERM.TYPES=SYSTEM(7)
    INCLUDE EB.OS.INCLUDES TERM.SETTINGS
    INCLUDE EB.OS.INCLUDES SKIP.PASSWORD
    READ EB.SECURITY.REC FROM FG_SECURITY,FG_LOGNAME ELSE
        READ EB.SECURITY.REC FROM FG_SECURITY,FG_TUSER ELSE EB.SECURITY.REC=''
    END
    IF LEN(EB.SECURITY.REC) NE 0 THEN
        UserName = EB.SECURITY.REC<1>
        FG_MONITOR.SECS=EB.SECURITY.REC<8>
        FG_TIMEOUT=EB.SECURITY.REC<9>*60
    END ELSE UserName = FG_LOGNAME
    MATREAD SCREEN.PARAMS FROM FG_EB.PARAMS,'CRT@':TERM.TYPE ELSE MAT SCREEN.PARAMS=''
    CURS.ON=SCREEN.PARAMS(22)
    CURS.OFF=SCREEN.PARAMS(23)
    CURS.BLOCK=SCREEN.PARAMS(54)
    CURS.LINE=SCREEN.PARAMS(55)
    CRT.COLS=SCREEN.PARAMS(48)
!    IF RegInput THEN
!        BG=''
!        FG=''
!    END
    MATREAD EB_CHARS FROM FG_EB.PARAMS,'EB.CHARS@':KEYBOARD.TYPE ELSE
        ABORT 202,'EB.PARAMS EB.CHARS@':KEYBOARD.TYPE
    END
    IF FG_BS.CH='' THEN FG_BS.CH=CHAR(8)
    FG_TERM.TYPE=TERM.TYPE
    FG_KEYBOARD=KEYBOARD.TYPE
    READ FG_MNENOMICS FROM FG_EB.PARAMS,'KEY.MNENOMICS@':FG_KEYBOARD ELSE FG_MNENOMICS=''
    FG_DISK.DRIVE=DISK.DRIVE<1,1>
    INCLUDE EB.INCLUDES INPUT.CODES
    IF FG_TIMEOUT ELSE FG_TIMEOUT=1200
    IF FG_MONITOR.SECS ELSE FG_MONITOR.SECS=FG_TIMEOUT
    READV FG_HOT.KEYS FROM FG_EB.CONTROL,FG_TUSER:'*HOT.KEYS',1 ELSE NULL
    READ FG_DAYS FROM FG_EB.PARAMS,'DAYS@':FG_LANGUAGE ELSE FG_DAYS=''
    READ FG_MONTHS FROM FG_EB.PARAMS,'MONTHS@':FG_LANGUAGE ELSE FG_MONTHS=''
    READ FUNCTIONS FROM FG_EB.PARAMS,'FUNC@':FG_KEYBOARD THEN
        FG_EXPECT.CR=FUNCTIONS<20>
        FG_FUNC.CHARS=FUNCTIONS<11>
        FG_FUNC.CHARS<1,-1>=FUNCTIONS<16>
    END
    READ FUNCTIONS FROM FG_EB.PARAMS,'FUNC@':FG_KEYBOARD:'@' THEN
        FG_FUNC.VALS=FUNCTIONS<9>
        FG_FUNC.VALS<1,-1>=FUNCTIONS<15>
    END
    READ EB.HELP FROM FG_EB.PARAMS, 'EB.HELP' THEN
        EB.HELP = CHANGE(EB.HELP, AM, VM)
    END ELSE EB.HELP = ''
    MAT CRT.PARAMS=''
    RETURN
 END
